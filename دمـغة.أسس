صنف دمـغة {
    عرف رسام: مؤشر[طـوب.رسـام] = 0؛
    عرف نقش: مؤشر[طـوب.نـقش] = 0؛
    عرف طول: صـحيح؛
    عرف عرض: صـحيح؛
    عرف شفافية: طـبيعي[8] = 255؛

    عملية هذا~هيئ () {
    }

    عملية هذا~هيئ (سند[دمـغة]) {
        هذا.رسام = قيمة.رسام؛
        هذا.نقش = قيمة.نقش؛
        هذا.طول = قيمة.طول؛
        هذا.عرض = قيمة.عرض؛
        هذا.شفافية = قيمة.شفافية؛
    }؛

    عملية هذا=سند[دمـغة] {
        هذا.رسام = قيمة.رسام؛
        هذا.نقش = قيمة.نقش؛
        هذا.طول = قيمة.طول؛
        هذا.عرض = قيمة.عرض؛
        هذا.شفافية = قيمة.شفافية؛
    }

    عملية هذا.هيئ (ر: مؤشر[طـوب.رسـام]، اسم_الملف: مـؤشر_محارف) {
        هذا.حرر()؛
        هذا.رسام = ر؛
        هذا.نقش = طـوب.صـورة.حمل_نقش(هذا.رسام، اسم_الملف)؛  
        إذا هذا.نقش == 0 {
            عرف رسالة: نـص = "فشل تحميل النقش: "؛
            رسالة += اسم_الملف؛
            رسالة += "\ج"؛
            طـرفية.اطبع(رسالة)؛
            اخرج(1)؛
        }
        طـوب.نـقش.استفهم(هذا.نقش، 0، 0، هذا.عرض~مؤشر، هذا.طول~مؤشر)؛
    }

    عملية هذا.هيئ (ر: مؤشر[طـوب.رسـام]، ن: مؤشر[طـوب.نـقش]) {
        هذا.حرر()؛
        هذا.رسام = ر؛
        هذا.نقش = ن؛
        طـوب.نـقش.استفهم(هذا.نقش، 0، 0، هذا.عرض~مؤشر، هذا.طول~مؤشر)؛
    }

    عملية هذا.حرر() {
        إذا هذا.نقش != 0 طـوب.نـقش.أتلف(هذا.نقش)؛
        هذا.نقش = 0؛
    }

    عملية هذا.اطمغ (س: صحيح، ص: صحيح) {
        عرف مستطيل: طـوب.مـستطيل؛
        مستطيل.س = س؛
        مستطيل.ص = ص؛
        مستطيل.ع = هذا.عرض؛
        مستطيل.ط = هذا.طول؛
        //طـوب.نـقش.غير_الشفافية(هذا.نقش، هذا.شفافية)؛
        طـوب.رسـام.انسخ(هذا.رسام، هذا.نقش، 0، مستطيل~مؤشر)؛
    }
}

